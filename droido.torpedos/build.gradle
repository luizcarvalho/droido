buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
    }
}
apply plugin: 'android'


sourceSets {
    unitTest {
        java.srcDir file('src/test/java')
        resources.srcDir file('src/test/res')
    }
}

configurations {
    unitTestCompile.extendsFrom runtime
    unitTestRuntime.extendsFrom unitTestCompile
}

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 19
    buildToolsVersion '19.0.0'

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 19
    }

    signingConfigs {
        release {
            storeFile file("../extra/rr-release-key.keystore")
            storePassword "swordfish"
            keyAlias "redrails"
            keyPassword "swordfish"
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }

}


dependencies {
    compile 'com.android.support:appcompat-v7:19.0.0'
    compile 'com.android.support:support-v4:19.0.0'
    compile files('src/main/libs/GoogleAdMobAdsSdk-6.4.1.jar')
    compile files('src/main/libs/libGoogleAnalyticsServices.jar')
    compile files('src/main/libs/Parse-1.4.2.jar')

    unitTestCompile files("$project.buildDir/classes/release")
    unitTestCompile 'junit:junit:4.10'
    unitTestCompile 'org.robolectric:robolectric:2.1.+'
    unitTestCompile 'com.google.android:android:4.0.1.2'

}

task unitTest(type:Test, dependsOn: assemble) {
    testClassesDir = project.sourceSets.unitTest.output.classesDir
    classpath = project.sourceSets.unitTest.runtimeClasspath
}
check.dependsOn unitTest
